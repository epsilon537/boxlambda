#TCL script used by the build system to create a Vivado project (BoxLambda uses
#Vivado project mode).
#
proc getopt {_argv name {_var ""} {default ""}} {
     upvar 1 $_argv argv $_var var
     set pos [lsearch -regexp $argv ^$name]
     if {$pos>=0} {
         set to $pos
         if {$_var ne ""} {
             set var [lindex $argv [incr to]]
         }
         set argv [lreplace $argv $pos $to]
         return 1
     } else {
         if {[llength [info level 0]] == 5} {set var $default}
         return 0
     }
}

#Command line arguments accepted by the script

# FPGA part number, e.g. xc7a35ticsg324-1L
getopt argv -part part "unknown"

# A TCL file generated by Bender containing all the verilog sources making
# up the project.
getopt argv -sources sources ""

# Project constraints file (mapping of top-level ports to pins etc.)
getopt argv -prj_constraints prj_constraints ""

# A TCL script containing a list of .mem files to add to the project.
getopt argv -mem_files mem_files ""

# A TCL script containing a list of all Vivado IPs to add to the project.
getopt argv -vivado_ips vivado_ips ""

# The project's top-level module.
getopt argv -top top ""

# A flag indicating if this is an out-of-context synthesis project.
getopt argv -ooc ooc ""

puts "part: $part"
puts "ooc: $ooc"

puts "sources: $sources"
puts "prj_constraints: $prj_constraints"
puts "mem_files: $mem_files"
puts "vivado_ips: $vivado_ips"
puts "top: $top"

#All projects are called project.xpr
create_project project.xpr -part $part -force
source $sources

#Only source the mem_file script if it's passed in and actually exists.
if {($mem_files != "") && ([file exists $mem_files] == 1)} {
    puts "sourcing mem files list."
    puts $mem_files
    source $mem_files
}

#Only source the prj_constraints script if it's passed in and actually exists.
if {($prj_constraints != "") && ([file exists $prj_constraints] == 1)} {
    puts "sourcing prj_constraints file list."
    puts $prj_constraints
    source $prj_constraints
}

#Only source the vivado_ips script if it's passed in and actually exists.
if {($vivado_ips != "") && ([file exists $vivado_ips] == 1)} {
    puts "sourcing Vivado IPs."
    puts $vivado_ips
    source $vivado_ips
}

if {$ooc>0} {
  set_property -name {STEPS.SYNTH_DESIGN.ARGS.MORE OPTIONS} -value {-mode out_of_context} -objects [get_runs synth_1]
}

set_property source_mgmt_mode None [current_project]

update_compile_order -fileset sources_1

set_property top $top [current_fileset]

#Suppress INFO messages
set_msg_config -severity INFO -suppress

close_project

